model {

# Priors
mean.p ~ dunif(0, 1)          # Detection intercept on prob. scale
alpha0 <- logit(mean.p)       #   same on logit scale
mean.psi ~ dunif(0, 1)        # Occupancy intercept on prob. scale
beta0 <- logit(mean.psi)      #   same on logit scale
for(k in 1:2){                # 1:K terms in detection model
   alpha[k] ~ dnorm(0, 0.1)   # Covariates on logit(detection)
}
for(k in 1:16){               # 1:K terms in occupancy model
   beta[k] ~ dnorm(0, 0.1)    # Covariates on logit(occupancy)
}

# Likelihood
for (i in 1:M) {
  z[i] ~ dbern(psi[i])
  logit(psi[i]) <- beta0 + inprod(beta[], occDM_hm[i,])
   for (j in 1:J) {
      y[i,j] ~ dbern(z[i] * p[i,j])
      logit(p[i,j]) <- alpha0 +     # detection (p) intercept
         alpha[1] * ord[i,j] +
         alpha[2] * dur[i,j]

   log.lik[i,j]  <- logdensity.bin(y[i,j],p[i,j],1) # for lppd

   }
   
   log.lik.cum[i] <- sum(log.lik[i,])
}

}